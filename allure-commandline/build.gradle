description = 'Allure Commandline'

apply plugin: 'application'
apply from: "${gradleScriptDir}/bintray.gradle"

applicationName = 'allure'
mainClassName = 'io.qameta.allure.CommandLine'

jar {
    manifest {
        attributes(
                'Implementation-Title': description,
                'Implementation-Version': version
        )
    }
}

startScripts {
    classpath += files('src/lib/config')
    doLast {
        unixScript.text = unixScript.text
                .replaceAll('(?m)^APP_HOME=', 'export APP_HOME=')
                .replaceAll('\\"\\$\\(uname\\)\\"\\s*=\\s*\\"Darwin\\"', '""')
    }
}

configurations {
    allureWeb
}

applicationDistribution.from(copyPlugins) {
    into 'plugins'
}

build.dependsOn installDist

dependencies {

    allurePlugin project(path: ':behaviors-plugin', configuration: 'allurePlugin')
//    allurePlugin project(path: ':junit-plugin', configuration: 'allurePlugin')
//    allurePlugin project(path: ':packages-plugin', configuration: 'allurePlugin')

    compile project(':allure-generator')
    compile('com.beust:jcommander')
    compile('org.eclipse.jetty:jetty-server')
    compile('org.slf4j:slf4j-api')
    compile('org.slf4j:slf4j-log4j12')

    testCompile('junit:junit')
    testCompile('org.assertj:assertj-core')
    testCompile('org.mockito:mockito-core')
    testCompile('org.apache.commons:commons-lang3')
}

configurations.archives.artifacts.removeAll { it.archiveTask.is jar }

bintray {
    pkg {
        repo = 'generic'
    }
}
