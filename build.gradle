import com.bmuschko.gradle.docker.tasks.image.Dockerfile

description = 'Allure Report'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE'
        classpath 'net.researchgate:gradle-release:2.6.0'
        classpath 'ru.vyarus:gradle-quality-plugin:2.4.0'
    }
}

ext {
    linkHomepage = 'https://qameta.io/allure'
    linkCi = 'https://ci.qameta.in/job/allure2_deploy/'
    linkScmUrl = 'https://github.com/allure-framework/allure2'
    linkScmConnection = 'scm:git:git://github.com/allure-framework/allure2.git'
    linkScmDevConnection = 'scm:git:ssh://git@github.com:allure-framework/allure2.git'
    gradleScriptDir = "${rootProject.projectDir}/gradle"
}


apply from: "${gradleScriptDir}/bintray.gradle"
apply from: "${gradleScriptDir}/release.gradle"

configure(rootProject) {
    description = "Allure Report"

    apply plugin: 'com.bmuschko.docker-remote-api'

    String root = rootProject.projectDir

    task build() {
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '4.4'
    }

    task deleteDemoReport(type: Delete) {
        delete "${root}/build/docker/report"
    }

    task generateDemoReport(type: Exec) {
        dependsOn = ['deleteDemoReport', 'allure-commandline:build']
        executable = "${root}/allure-commandline/build/install/allure/bin/allure"
        args = ['generate', "${root}/allure-web/test-data/demo", '-o', "${root}/build/docker/report"]
    }

    task generateDemoDockerfile(type: Dockerfile) {
        dependsOn = ['generateDemoReport']
        destFile project.file('build/docker/Dockerfile')

        from 'nginx'
        maintainer 'Dmitry Baev <baev@qameta.io>'
        addFile 'report', '/usr/share/nginx/html'
    }
}

allprojects {
    group = project.name.endsWith('plugin') ? 'io.qameta.allure.plugins' : 'io.qameta.allure'
    version = version

    configurations {
        allurePlugin
    }

    tasks.withType(Tar) {
        compression = Compression.GZIP
    }

    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        imports {
            mavenBom 'com.fasterxml.jackson:jackson-bom:2.9.3'
        }
        dependencies {
            dependency 'com.beust:jcommander:1.72'
            dependency 'com.google.guava:guava:23.5-jre'
            dependency 'com.opencsv:opencsv:4.1'
            dependency 'com.vladsch.flexmark:flexmark:0.28.18'
            dependency 'commons-io:commons-io:2.6'
            dependency 'io.qameta.allure:allure-java-commons:2.0-BETA21'
            dependency 'io.qameta.allure:allure2-model-api:1.0-BETA4'
            dependency 'io.qameta.allure:allure-model:2.0-SNAPSHOT'
            dependency 'javax.xml.bind:jaxb-api:2.3.0'
            dependency 'junit:junit:4.12'
            dependency 'org.allurefw:allure1-model:1.0'
            dependency 'org.apache.commons:commons-lang3:3.7'
            dependency 'org.apache.commons:commons-text:1.1'
            dependency 'org.apache.httpcomponents:httpclient:4.5.4'
            dependency 'org.apache.tika:tika-core:1.16'
            dependency 'org.assertj:assertj-core:3.8.0'
            dependency 'org.eclipse.jetty:jetty-server:9.4.8.v20171121'
            dependency 'org.freemarker:freemarker:2.3.23'
            dependency 'org.mockito:mockito-core:2.13.0'
            dependency 'org.projectlombok:lombok:1.16.18'
            dependency 'org.slf4j:slf4j-api:1.7.25'
            dependency 'org.slf4j:slf4j-log4j12:1.7.25'
            dependency 'org.slf4j:slf4j-nop:1.7.25'
            dependency 'org.slf4j:slf4j-simple:1.7.25'
            dependency 'org.zeroturnaround:zt-zip:1.12'
        }
    }

    repositories {
        mavenLocal()
        jcenter()
    }
}

subprojects { project ->
    apply plugin: 'java'
    compileJava.dependsOn(processResources)

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
        options.compilerArgs += "-parameters"
    }

    jar {
        manifest {
            attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
        }
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
    }

    configurations {
        allurePlugin
    }

    task copyPlugins(type: Sync) {
        dependsOn configurations.allurePlugin
        from {
            configurations.allurePlugin.collect { zipTree(it) }
        }
        into "$buildDir/plugins/"
        eachFile { FileCopyDetails details ->
            def segments = details.relativePath.segments
            segments[0] = segments[0].replace("-${project.version}", '')
        }
        includeEmptyDirs = false
    }

    apply plugin: 'ru.vyarus.quality'

    quality {
        checkstyleVersion = '7.6.1'
        pmdVersion = '5.8.1'
        findbugsVersion = '3.0.1'
        codenarcVersion = '0.26.0'
        excludeSources = fileTree('build/generated-sources')
    }

    apply plugin: 'jacoco'

    jacoco {
        toolVersion = '0.7.9'
    }
}
