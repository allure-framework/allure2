import com.bmuschko.gradle.docker.tasks.image.Dockerfile

buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.2.RELEASE'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.jacobono:gradle-jaxb-plugin:1.3.6'
        classpath 'com.moowork.gradle:gradle-node-plugin:1.1.1'
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.4'
        classpath 'net.researchgate:gradle-release:2.6.0'
        classpath 'ru.vyarus:gradle-quality-plugin:2.2.0'
    }
}

ext {
    linkHomepage = 'https://qameta.io/allure'
    linkCi = 'https://ci.qameta.in/job/allure2_deploy/'
    linkScmUrl = 'https://github.com/allure-framework/allure2'
    linkScmConnection = 'scm:git:git://github.com/allure-framework/allure2.git'
    linkScmDevConnection = 'scm:git:ssh://git@github.com:allure-framework/allure2.git'
    gradleScriptDir = "${rootProject.projectDir}/gradle"
}


apply from: "${gradleScriptDir}/bintray.gradle"
apply from: "${gradleScriptDir}/release.gradle"

configure(rootProject) {
    description = "Allure Report"

    apply plugin: 'com.bmuschko.docker-remote-api'

    String root = rootProject.projectDir

    task build() {
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '3.5'
    }

    task deleteDemoReport(type: Delete) {
        delete "${root}/build/docker/report"
    }

    task generateDemoReport(type: Exec) {
        dependsOn = ['deleteDemoReport', 'allure-commandline:build']
        executable = "${root}/allure-commandline/build/install/allure/bin/allure"
        args = ['generate', "${root}/allure-web/test-data/demo", '-o', "${root}/build/docker/report"]
    }

    task generateDemoDockerfile(type: Dockerfile) {
        dependsOn = ['generateDemoReport']
        destFile project.file('build/docker/Dockerfile')

        from 'nginx'
        maintainer 'Dmitry Baev <baev@qameta.io>'
        addFile 'report', '/usr/share/nginx/html'
    }
}

allprojects {
    group = project.name.endsWith('plugin') ? 'io.qameta.allure.plugins' : 'io.qameta.allure'
    version = version

    configurations {
        allurePlugin
    }

    tasks.withType(Tar) {
        compression = Compression.GZIP
    }

    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        dependencies {
            dependency 'org.apache.tika:tika-core:1.14'
            dependency 'commons-io:commons-io:2.5'
            dependency 'com.google.guava:guava:21.0'
            dependency 'com.beust:jcommander:1.64'
            dependency 'org.zeroturnaround:zt-zip:1.11'
            dependency 'org.apache.commons:commons-lang3:3.5'
            dependency 'org.eclipse.jetty:jetty-server:9.2.2.v20140723'
            dependency 'org.pegdown:pegdown:1.6.0'
            dependency 'org.freemarker:freemarker:2.3.23'

            dependency 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:1.11.1'
            dependency 'org.jvnet.jaxb2_commons:jaxb2-basics:1.11.1'
            dependency 'com.github.jaxb-xew-plugin:jaxb-xew-plugin:1.8'
            dependency 'net.java.dev.jaxb2-commons:jaxb-fluent-api:2.1.8'

            dependency 'org.allurefw:allure1-model:1.0'
            dependency 'io.qameta.allure:allure2-model-api:1.0-BETA4'
            dependency 'net.masterthought:cucumber-reporting:3.4.0'
            dependency 'org.apache.maven.surefire:surefire-report-parser:2.19.1'

            dependency 'com.fasterxml.jackson.core:jackson-databind:2.8.6'
            dependency 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.8.6'
            dependency 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.8.6'

            dependency 'org.slf4j:slf4j-nop:1.7.21'
            dependency 'org.slf4j:slf4j-api:1.7.21'
            dependency 'org.slf4j:slf4j-simple:1.7.21'
            dependency 'org.slf4j:slf4j-log4j12:1.7.21'

            dependency 'junit:junit:4.12'
            dependency 'org.assertj:assertj-core:3.6.2'
            dependency 'org.mockito:mockito-core:2.7.19'
        }
    }

    repositories {
        jcenter()
    }
}

subprojects { project ->
    apply plugin: 'java'
    compileJava.dependsOn(processResources)

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
        options.compilerArgs += "-parameters"
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
    }

    configurations {
        allurePlugin
    }

    task copyPlugins(type: Sync) {
        dependsOn configurations.allurePlugin
        from {
            configurations.allurePlugin.collect { zipTree(it) }
        }
        into "$buildDir/plugins/"
        eachFile { FileCopyDetails details ->
            def segments = details.relativePath.segments
            segments[0] = segments[0].replace("-${project.version}", '')
        }
        includeEmptyDirs = false
    }

    apply plugin: 'ru.vyarus.quality'

    quality {
        checkstyleVersion = '7.6'
        pmdVersion = '5.5.4'
        findbugsVersion = '3.0.1'
        codenarcVersion = '0.26.0'
        excludeSources = fileTree('build/generated-sources')
    }
}
